/** Absolute minimum build to be able to publish when the plugin-plugin is not available */
import org.gradle.api.publish.maven.MavenPublication

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3' }
}

repositories { jcenter() }

apply plugin: 'groovy'
apply plugin: 'idea'

group = 'com.netflix.nebula'

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

apply plugin: 'bintray'
def packageName = name
bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        userOrg = 'nebula'
        name = packageName
        licenses = ['Apache-2.0']
    }
    // Will have to manually publish on bintray.com
}
bintrayUpload.dependsOn(build)

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
    logger.info "Installed $project.name"
}

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    // Dynamic versions are ok, the nebula-maven-publishing plugin will take care of them
    compile 'org.apache.commons:commons-lang3:3.1'
    compile('org.redline-rpm:redline:1.2.1') {
        // Where logging foes is a runtime decision
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compile('org.vafer:jdeb:1.0.1') {
        // Individual listing so that they translate into the pom file
        exclude group: 'org.apache.maven', module: 'maven-core'
        exclude group: 'org.apache.maven', module: 'maven-plugin-api'
        exclude group: 'org.apache.maven', module: 'maven-project'
        exclude group: 'org.apache.maven', module: 'maven-artifact'
        exclude group: 'org.codehaus.plexus', module: 'plexus-utils'
    }

    testCompile('com.netflix.nebula:nebula-test:2.2.0-SNAPSHOT') {
        exclude group: 'org.codehaus.groovy'
    }
}

test.exclude '**/*Local*'

task createWrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
