buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.netflix.nebula:nebula-plugin-plugin:1.9.3' }
}

description 'Provides a task similar to Tar and Zip for constructing RPM and DEB package files.'
apply plugin: 'nebula-plugin'

apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    // Dynamic versions are ok, teh nebula-maven-publishing plugin will take care of them
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'org.redline-rpm:redline:1.1.12'
    compile('org.vafer:jdeb:1.0.1') {
        // Individual listing so that they translate into the pom file
        exclude group: 'org.apache.maven', module: 'maven-core'
        exclude group: 'org.apache.maven', module: 'maven-plugin-api'
        exclude group: 'org.apache.maven', module: 'maven-project'
        exclude group: 'org.apache.maven', module: 'maven-artifact'
        exclude group: 'org.codehaus.plexus', module: 'plexus-utils'
    }

    testCompile 'junit:junit:4.11'
    testCompile([group: 'org.gmock', name: 'gmock', version: '0.8.2', transitive: false])
}

def javaApiUrl = 'http://docs.oracle.com/javase/1.6.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'
tasks.withType(Javadoc) {
    options.links(javaApiUrl, groovyApiUrl)
}

test {
  testLogging {
    exceptionFormat "full"
  }
}

def pomConfig = {
    inceptionYear '2011' // as gradle-rpm-plugin

    developers {
        developer {
            id 'akrueger'
            name 'Alan Krueger'
            email 'alan@trigonic.com'
            roles{
                role 'Developer'
            }
            timezone '-6'
        }
        developer {
            id 'quidryan'
            name 'Justin Ryan'
            email 'justin@halfempty.org'
            roles{
                role 'Developer'
            }
            timezone '-8'
        }
    }
}

import nebula.plugin.publishing.maven.NebulaBaseMavenPublishingPlugin
plugins.withType(NebulaBaseMavenPublishingPlugin) {
    withMavenPublication {
        pom.withXml {
            asNode().children().last() + pomConfig
        }
    }
}

